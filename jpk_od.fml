:!UTF-8

\jpk_decl
:: deklaracja objektu jpk 
{? var_pres('JPKCLASS',@.CLASS)<0 ||
   obj_decl('JPKCLASS',
         obj_fld('File',0),
		 obj_fld('Fname','jpk_kr_'),
		 obj_fld('Fpath','c:\\jpk\\jpk_vat'),
		 obj_fld('KodForm','JPK_KR'),
		 obj_fld('KodSys','JPK_KR (1)'),
		 obj_fld('WerSchem','1-0'),
		 obj_fld('WarForm','1'),
		 obj_fld('CelZl','1'),
		 obj_fld('DataWyt',date),
		 obj_fld('DataOd',date),
		 obj_fld('DataDo',date),
		 obj_fld('KodWal','PLN'),
		 obj_fld('KodUs','1209'), 
         obj_meth('__init',"@.STALE.AO(); 
		    .DataOd:=.TData(form(OKRO_F.POCZ));
			.DataDo:=.TData(form(OKRO_F.KON));
			.DataWyt:=.TDataTime(date,time)"),
		 obj_meth('__done',""),
		 obj_meth('TData',"_a:=form(_a);(4+_a)+'-'+(2+(5-_a))+'-'+(_a+2)"),
		 obj_meth('TDataTime',".TData(form(_a))+'T'+form(_b,,3)"),
		 obj_meth('TKwota',"form(_a,,2,'9.')"),
         obj_meth('toxml',"
		    {? _=1 || _b:=_c:=~~ |? _=2 || _c:=~~ ?};
			{? var_pres('_a')<0 || _a:='error' ?};
            {? var_pres('_b')<=0 || _b:='' ?};
			_wiersz:='<'+_a+{? _b<>''||' '+_b||''?}+'>'+{? var_pres('_c')>0 || maz_utf8(@.STR.gsub(_c,'&',' '))+'</'+_a+'>' || '' ?};
            {? var_pres('_c')>0 & _c<>'' || fwrite(.File,_wiersz)
            |? var_pres('_c')<=0 || fwrite(.File,_wiersz)
            || 1
            ?};1"),
         obj_meth('podmiot',".toxml('Podmiot1');
            .toxml('IdentyfikatorPodmiotu');
            _nip:=@.STR.gsub(@.FINFO.NIP,'-',''); 
            .toxml('etd:NIP',,_nip);
            .toxml('etd:PelnaNazwa',,@.FINFO.NAZ);
            .toxml('etd:REGON',,@.FINFO.REGON);
            .toxml('/IdentyfikatorPodmiotu');
            .toxml('AdresPodmiotu');
            .toxml('etd:KodKraju',,@.FINFO.KRAJ().KODISO);
            .toxml('etd:Wojewodztwo',,@.FINFO.WOJ().NAZWA);
            .toxml('etd:Powiat',,@.FINFO.POWIAT);
            .toxml('etd:Gmina',,@.FINFO.GMINA);
            .toxml('etd:Ulica',,@.FINFO.UL);
            .toxml('etd:NrDomu',,@.FINFO.NR_D);
            .toxml('etd:NrLokalu',,@.FINFO.NR_L);
            .toxml('etd:Miejscowosc',,@.FINFO.MIA);
            .toxml('etd:KodPocztowy',,@.FINFO.KOD_P);
            .toxml('etd:Poczta',,@.FINFO.POCZ);
            .toxml('/AdresPodmiotu');
            .toxml('/Podmiot1')"),
         obj_meth('start',".Fname:=-.KodForm+(date$0);
		    undefine();
			define('PATH',.Fpath,utf8_maz('Ścieżka'),,60,60);
            define('FILE',.Fname,'Nazwa pliku',,20,20);
			{? def_edit() ||
               .Fname:={? DEFINE.FILE*'.'>0 || (DEFINE.FILE*'.')+DEFINE.FILE+'xml'|| DEFINE.FILE+'.xml' ?};
               .Fpath:=DEFINE.PATH;
               .File:=fopen('@'+.Fpath+'\\\\'+.Fname,'w');
               .toxml('?xml version=\"1.0\" encoding=\"UTF-8\"?');
               .toxml('JPK','xmlns=\"http://jpk.mf.gov.pl/wzor/2016/03/09/03091/\" xmlns:etd=\"http://crd.gov.pl/xml/schematy/dziedzinowe/mf/2016/01/25/eD/DefinicjeTypy/\" xmlns:xsd=\"http://www.w3.org/2001/XMLSchema\"');
               1
            || 0 
            ?}"),
         obj_meth('stop',".toxml('/JPK');
            fclose(.File); msg('Plik '+.Fname+utf8_maz(' został wygenerowany pomyślnie'));1"),
         obj_meth('naglowek',".toxml('Naglowek');
            .toxml('KodFormularza','wersjaSchemy=\"'+.WerSchem+'\" kodSystemowy=\"'+.KodSys+'\"',.KodForm);
            .toxml('WariantFormularza',,.WarForm);
            .toxml('CelZlozenia',,.CelZl);
            .toxml('DataWytworzeniaJPK',,.DataWyt);
            .toxml('DataOd',,.DataOd);
            .toxml('DataDo',,.DataDo);
            .toxml('DomyslnyKodWaluty',,'PLN');
            .toxml('KodUrzedu',,'1209');
            .toxml('/Naglowek');1")			
           );1
||
   0
?}

\jpk_kr
:: Deklaracja objektu

exec('jpk_decl','jpk_od');
JPK:=obj_new(@.CLASS.JPKCLASS);

:: ustawienie zmiennnych JPK

JPK.KodForm:='JPK_KR';
JPK.KodSys:='JPK_KR (1)';
JPK.WerSchem:='1-0';
JPK.WarForm:='1';
JPK.CelZl:='1';

{? JPK.start() ||
   JPK.naglowek();
   JPK.podmiot();
:: ------begine body ------
   exec('jpk_zois','jpk_od');
   exec('jpk_dzie','jpk_od');
   exec('jpk_zap','jpk_od');
:: ------- end body -------
   JPK.stop();
   1
|| msg('Plik nie została wygenerowany!!') 
?};
obj_del(JPK)

\jpk_zois
:: zestawienie obrotów i sald do JPK_KR
:: objekt JPK zadeklarowany w nagłówku

AN.cntx_psh();AN.index('WALSYM');AN.prefix(SSTALE.WAL);
{? AN.first || 
{! |?
      
	  JPK.toxml('ZOiS','typ=\"G\"');
      JPK.toxml('KodKonta',,AN.SYM);
	  exec('pw_opan','obr_an');
	  _ilepoz:=1;
	  BUD.cntx_psh();BUD.index('KS'); BUD.prefix(AN.KS); _ilepoz+=BUD.size; BUD.cntx_pop();
	  _op_kon:=$('KA.O'+form(_ilepoz));
	  {? _op_kon()='-' & _ilepoz>1 || _op_kon:=$('KA.O'+form(_ilepoz-1)) ?}; 
      JPK.toxml('OpisKonta',,_op_kon());
      JPK.toxml('TypKonta',,exec('typ_ks','jpk_od',AN.KS().TYP));
      JPK.toxml('KodZespolu',,1+AN.SYM);
      JPK.toxml('OpisZespolu',,exec('zespol','jpk_od',1+AN.SYM));
      JPK.toxml('KodKategorii',,AN.KS().SYM);
      JPK.toxml('OpisKategorii',,AN.KS().NAZ);
	  {? +AN.SYM>3 ||
         JPK.toxml('KodPodkategorii',,4-AN.SYM);
         JPK.toxml('OpisPodkategorii',,_op_kon())
	  ?};
      JPK.toxml('BilansOtwarciaWinien',,JPK.TKwota(F.Wn(0,0)));
      JPK.toxml('BilansOtwarciaMa',,JPK.TKwota(F.Ma(0,0)));
      JPK.toxml('ObrotyWinien',,JPK.TKwota(F.Wn(OKRO_F.POCZ ~2,OKRO_F.KON~2)));
      JPK.toxml('ObrotyMa',,JPK.TKwota(F.Wn(OKRO_F.POCZ~2,OKRO_F.KON~2)));
	  _wn:=F.Wn(0,OKRO_F.KON~2); _ma:=F.Ma(0,OKRO_F.KON~2);
      JPK.toxml('ObrotyWinienNarast',,JPK.TKwota(_wn));
      JPK.toxml('ObrotyMaNarast',,JPK.TKwota(_ma));
      JPK.toxml('SaldoWinien',,JPK.TKwota(F.SWn(_wn,_ma)));
      JPK.toxml('SaldoMa',,JPK.TKwota(F.SMa(_wn,_ma)));
      JPK.toxml('/ZOiS');
      AN.next
!}
?};
AN.cntx_pop

\zespol
:: opis zespołu kont wg wykazu kont bilansowych
:: _a kod zespołu kont [String]

{? _a='0' || 'Aktywa trwałe'
|? _a='1' || 'Środki pieniężne i rachunki bankowe'
|? _a='2' || 'Rozrachunki i roszczenia'
|? _a='3' || 'Materiały i towary'
|? _a='4' || 'Koszty według rodzajów i ich rozliczenie'
|? _a='5' || 'Koszty według typów działalności i ich rozliczenie'
|? _a='6' || 'Produkty i rozliczenia międzyokresowe'
|? _a='7' || 'Przychody i koszty związane z ich osiągnięciem'
|? _a='8' || 'Kapitały ( fundusze) własne, fundusze specjalne i wynik finansowy'
|? _a='9' || 'Pozabilansowe'
|| ''
?}

\typ_ks
:: typ konta _a=literowy typ konta

{? _a='PB' || 'Pozabilansowe'
|? _a='BL' || 'Bilansowe'
|? _a='BW' || 'Wynikowe'
|? _a='BN' || 'Bezwynikowe'
|? _a='P1' || 'Pozabilansowe - jednoroczne'
|| 'BRAK'
?}

\jpk_dzie
:: dziennik 

DOK.cntx_psh; POZ.cntx_psh;
DOK.index('NRDZ2'); POZ.index('DOK');
DOK.prefix('T','T');
_lp:=0;_total:=0;
{? DOK.first ||
   {! |? _lp+=1; 	
         JPK.toxml('Dziennik','typ=\"G\"');
		 JPK.toxml('LpZapisuDziennika',,$_lp);
         JPK.toxml('NrZapisuDziennika',,DOK.REJ().KOD+'/'+$DOK.NRDZ);
         JPK.toxml('OpisDziennika',,DOK.REJ().NAZ);
         JPK.toxml('NrDowoduKsiegowego',,DOK.NK);
         JPK.toxml('RodzajDowodu',,DOK.DOK_REJ().NAZ);
         JPK.toxml('DataOperacji',,JPK.TData(DOK.DTW));
         JPK.toxml('DataDowodu',,JPK.TData(DOK.DTO));
         JPK.toxml('DataKsiegowania',,JPK.TData(DOK.DKS));
         JPK.toxml('KodOperatora',,DOK.KSIEGOWY);
         JPK.toxml('OpisOperacji',,DOK.TR);
		 _kw:=0; POZ.prefix(DOK.ref); 
		 {? POZ.first || {! |? {? 1+POZ.STR='M'|| _kw+=POZ.SUM ?}; POZ.next !} ?};
		 _total+=_kw;
         JPK.toxml('DziennikKwotaOperacji',,JPK.TKwota(_kw));
		 JPK.toxml('/Dziennik');
		 DOK.next
    !}
?};
POZ.cntx_pop; DOK.cntx_pop;
JPK.toxml('DziennikCtrl');
JPK.toxml('LiczbaWierszyDziennika',,$_lp);
JPK.toxml('SumaKwotOperacji',,JPK.TKwota(_total));
JPK.toxml('/DziennikCtrl');
1
    
\jpk_zap
:: zapisy

DOK.cntx_psh; POZ.cntx_psh;
DOK.index('NRDZ2'); POZ.index('DOK');
DOK.prefix('T','T');
_lp:=0;_totalw:=_totalm:=0;
{? DOK.first ||
   {! |?  
      _nrzap:=DOK.REJ().KOD+'/'+$DOK.NRDZ;
      POZ.prefix(DOK.ref); 
      {? POZ.first ||  
         {! |?	
		 _kww:=_kwm:=0;_lp+=1; 
		 _ktw:=_ktm:='null';_opw:=_opm:='';
         JPK.toxml('KontoZapis','typ=\"G\"');
		 JPK.toxml('LpZapisu',,$_lp);
         JPK.toxml('NrZapisu',,_nrzap);
		 {? 1+POZ.STR='W'|| 
		    _kww:=POZ.SUM;
			_ktw:=POZ.KON;
			_opw:=POZ.OP;
		    _totalw+=_kww
		 || 
		    _kwm:=POZ.SUM;
			_ktm:=POZ.KON;
			_opm:=POZ.OP;
		    _totalm+=_kwm
		 ?};  
		 JPK.toxml('KodKontaWinien',,_ktw);
		 JPK.toxml('KwotaWinien',,JPK.TKwota(_kww));
		 JPK.toxml('OpisZapisuWinien',,_opw);
		 JPK.toxml('KodKontaMa',,_ktm);
		 JPK.toxml('KwotaMa',,JPK.TKwota(_kwm));
		 JPK.toxml('OpisZapisuMa',,_opm);
		 JPK.toxml('/KontoZapis');
		 POZ.next !}
	  ?};
	  DOK.next
    !}
?};
POZ.cntx_pop; DOK.cntx_pop;
JPK.toxml('KontoZapisCtrl');
JPK.toxml('LiczbaWierszyKontoZapisj',,$_lp);
JPK.toxml('SumaWinien',,JPK.TKwota(_totalw));
JPK.toxml('SumaMa',,JPK.TKwota(_totalm));
JPK.toxml('/KontoZapisCtrl');
1

\jpk_wb
:: JPK WB

:: Deklaracja objektu

exec('jpk_decl','jpk_od');
JPK:=obj_new(@.CLASS.JPKCLASS);

:: ustawienie zmiennnych JPK

JPK.KodForm:='JPK_WB';
JPK.KodSys:='JPK_WB (1)';
JPK.WerSchem:='1-0';
JPK.WarForm:='1';
JPK.CelZl:='1';

{? JPK.start() ||
   JPK.naglowek();
   JPK.podmiot();
:: ------begine body ------
:: NumerRachunku
   SKID_RBK.cntx_psh;
   SKID.index('RD'); SKID_RBK.prefix('INFO');
   _nrach:='BRAK';
   {? SKID_RBK.first ||
      {! |? {? SKID_RBK.RD='T' || _nrach:=SKID_RBK.N;0 || 1 ?} & SKID_RBK.next !}
   ?}
   SKID_RBK.cntx_pop;
   JPK.toxml('NumerRachunku',,_nrach);
:: ------- end body -------
   JPK.stop();
   1
|| msg('Plik nie została wygenerowany!!') 
?};
obj_del(JPK)

